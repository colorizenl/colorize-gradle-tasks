buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	
	dependencies {
		classpath 'com.gradle.publish:plugin-publish-plugin:0.9.4'
	}
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'jacoco'
apply plugin: 'eclipse'

group = 'nl.colorize'
version = '2017.2'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'
sourceSets.main.java.srcDirs = ['source']
sourceSets.test.java.srcDirs = ['source.test']
jar.archiveName = "colorize-gradle-tasks-${version}.jar"

repositories {
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	testCompile 'junit:junit:4.12'
}

jar.doLast {
	copy {
		from jar.archivePath
		into "${buildDir}/libs"
		rename jar.archiveName, 'colorize-gradle-tasks.jar'
	}
}

test.doLast {
	tasks.jacocoTestReport.execute()
}

gradlePlugin {
	plugins {
		webAppPlugin {
			id = 'nl.colorize.gradle.webapp'
			implementationClass = 'nl.colorize.gradle.webapp.WebAppPlugin'
		}
	}
}

pluginBundle {
	website = 'http://www.colorize.nl/en/'
	vcsUrl = 'https://github.com/colorizenl/colorize-gradle-tasks'
	description = 'Gradle plugin for building HTML/CSS/JavaScript web applications.'
	tags = ['webapp', 'webapplication', 'html', 'css', 'javascript', 'js']
	
	plugins {
		webAppPlugin {
			id = 'nl.colorize.gradle.webapp'
			displayName = 'HTML/CSS/JavaScript web application plugin'
		}
	}
	
	mavenCoordinates {
		groupId = 'nl.colorize'
		artifactId = 'gradle-webapp-plugin'
	}
}
